import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.6.2'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version "${springBootVersion}" apply false
}

ext['log4j2.version'] = '2.17.1'
ext['reactor-bom.version'] = '2020.0.15'

allprojects {

    apply plugin: 'idea'
    apply plugin: 'java-library'

    repositories {
        mavenLocal()
        mavenCentral()
    }
    group 'br.com.jfr.libs'
    version '0.0.1-SNAPSHOT'

    configurations {
    }
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'io.projectreactor:reactor-test'
    }

    sourceCompatibility = '11'
    targetCompatibility = '11'

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    publishing {
        publications {
            "$project.name"(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
